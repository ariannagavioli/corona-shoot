#define START_WALK	 	16
#define END_WALK 		48
#define GRAVITY			9.8

/* Player class */
class Player{
	
	var upper_mesh;
	var upper_object;
	
	var lower_mesh;
	var lower_object;
	
	var weapon_mesh;
	var weapon_object;
	
	var player_object;
	
	var position;				//[x,y,z] point in space of the player
	var angle;					//determines the direction the player is facing
	var step_velocity;			//how much player moves in one frame over the plane x-z (absolute value)
	var current_velocity;		//current player velocity (is signed, x-z)
	var step_angle;				//delta angle of one frame: how much rotation can be done in one frame
	var jumping_velocity;		//velocity over the y-axis
	
	var is_jumping;
	
	var current_frame;			//current frame of the mesh
	
	var life_points;
	
	init();
	draw();
	setPosition(pos,ang);
	update_jump();
	update();
};

function player::init(){
	lower_mesh = CVmNewMesh("swat_lower.AAM");
	upper_mesh = CVmNewMesh("swat_upper.AAM");
	weapon_mesh = CVmNewMesh("rocketl.AAM");
	
	var scale_measure = lower_mesh.normalize(1);
	upper_mesh.scale(scale_measure);
	weapon_mesh.scale(scale_measure);
	
	lower_object = CVmObj(lower_mesh);
	upper_object = CVmObj(upper_mesh);
	weapon_object = CVmObj(weapon_mesh);
	
	player_object = CVmObj();
	
	player_object.addChild(lower_object);
	player_object.addChild(upper_object);
	player_object.addChild(weapon_object);
	
	angle = 0;
	position = [0,0,0];		//[x,y,z]
	step_velocity = 0.08;
	step_angle = 1.0;	
	current_velocity = 0;
	current_frame = 0;
	
	/* Player is not jumping */
	jumping_velocity = 0;
	is_jumping = false;
	
	life_points = 100;
	
	setPosition(position,angle);
}

function player::setPosition(pos,ang){
	position = pos;
	player_object.setPosition(position);
	player_object.setRotation(ang,[0,1,0]);	//rotate the player around y-axis
}

function player::update_jump(){
	
	var delta = 0.03;
		
	position[1] += jumping_velocity * delta;
	jumping_velocity -= GRAVITY * delta;
	
	if(position[1] <= 0){
		is_jumping = false;
		position[1] = 0;
	}
}

function player::update()
{
	current_velocity = 0;
	
	if (keypressed(VK_UP))
		current_velocity = step_velocity;
	else if (keypressed(VK_DOWN))
		current_velocity = -step_velocity;	
	
	if (keypressed(VK_RIGHT)){
		angle -= step_angle;
		angle = angle % 360;
	}
    else if(keypressed(VK_LEFT)){
        angle += step_angle;
    	angle = angle % 360;  
    }
	
    /* Jump, only if not already jumping*/
    if( (keypressed(VK_SPACE) && !is_jumping)){
    	is_jumping = true;
    	jumping_velocity = 5.0;
    }
    
    /* If player is jumping, update jump dynamics*/
    if(is_jumping){
    	update_jump();
    }
		
	if(current_velocity != 0){
		current_frame += 1;
		current_frame = current_frame % (END_WALK - START_WALK);
	}
	
    
    position[0] += current_velocity * sin(angle / 180.0 * PI);	// x-component
    position[2] += current_velocity * cos(angle / 180.0 * PI);	// z-component
	
//	var x = position[0];
//	trace(x);
	
	setPosition(position, angle);		
}

function player::draw()
{
	if(current_velocity != 0)
		player_object.draw(VR_FRAMENUMBER,START_WALK + current_frame);
	else
		player_object.draw(VR_FRAMENUMBER,0);
}

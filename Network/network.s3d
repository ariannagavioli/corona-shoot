/* Network.s3d */


// PDU Definition Data exchange
#define TROOPER_KINEMATICS_PDU	1
#define BULLET_KINEMATICS_PDU	2
#define FIRE_AMMO_PDU			3
#define AMMO_LIFE_ENDED			4
#define GOT_SHOT_PDU 			5
#define WEAPON_PDU				6
#define WEAPON_SWITCH_PDU		7
#define JUMP_SCREAM_PDU			8
#define INJURIED_PDU			9
#define WINNER_PDU				10

// PDU Definition Autentication
#define NAME_ME_PDU		99 	// Command to ask the server for a name
#define LIST_RQ_PDU		98	// Command to ask the server for a list of players
#define LIST_RM_PDU		97	// Command from the server to remove a client

class PDUClass {
	var msgPosition;	// PDU to send position
	var msgInjuried;	// PDU to send who was injuried
	var msgWinner;		// PDU to send the winner
	var msgEvent;		// PDU to send events 
	var command2;		// PDU for sending commands to the Server

	kinematics(pdu_type, pos, vel, angle);	// generate trooper position pdu
	event(event_type);
	Injuried(who, dammage);
	Winner(win);
};

function PDUClass::Winner(win) {
	msgWinner[2] = win;
}

function PDUClass::Injuried(who, dammage) {
	msgInjuried[2] = who;
	msgInjuried[3] = dammage;
}

function PDUClass::event(event_type) {
	msgEvent[0] = event_type;
}

function PDUClass::kinematics(pdu_type, pos, vel, angle) {
	msgPosition[0] = pdu_type;
	msgPosition[2] = pos[0];
	msgPosition[3] = pos[1];
	msgPosition[4] = pos[2];
	msgPosition[5] = vel;
	msgPosition[6] = 0;		// for future vertical velocity
	msgPosition[7] = angle;
}

function PDUClass::PDUClass(id) {
	// pdu to send a command to the server
	command2 = vector(2);
	command2[1] = id;

	// Position PDU
	msgPosition = vector(8);
	msgPosition[0] = TROOPER_KINEMATICS_PDU;
	msgPosition[1] = id;

	// Event PDU
	msgEvent = vector(3);
	msgEvent[0] = WEAPON_SWITCH_PDU;
	msgEvent[1] = id;
	msgEvent[2] = 1;

	// Injuried PDU
	msgInjuried = vector(4);
	msgInjuried[0] = INJURIED_PDU;
	msgInjuried[1] = id;

	// Winner PDU
	msgWinner = vector(3);
	msgWinner[0] = WINNER_PDU;
	msgWinner[1] = id;
}

class Network {
	var m_id;
	var m_badass_name;
	var m_pdu;
	var channel;
	var dataRcv;
	var open;

	connectTCP(ip, port);
	receiveTCP(data);
	force_receiveTCP(data); // it does busy waiting to wait until have a response
	sendTCP(data);
	closeTCP();
};

function Network::Network() {
	open = false;	
}

function Network::connectTCP(ip, port) {
	channel = NetConnectTCP(ip, port, VR_NO_BLOCKING);
	open = true;
}

function Network::force_receiveTCP(data) {
	data = NULL;
	while(data == NULL)
		data = NetVarReceiveFromTCP(channel, "");
}

function Network::receiveTCP(data) {
	if (open)
		data = NetVarReceiveFromTCP(channel, "");
}

function Network::sendTCP(data) {
	if (open)
		NetVarSendToTCP(channel, data, "");
}

function Network::closeTCP() {
	if (open) {
		NetClose(channel);
		open = false;
	}
}

/* Level class */
class level {
	/* Walls */
	var wall_mesh;
	var lower_wall_mesh;
	var wall_obj_array;
	var wall_material;
	var wall_texture;
	
	/* Floor */
	var floor_mesh;
	var floor_material;
	var floor_texture;
	
	/* Sky */
	var sky_mesh;
	var sky_material;
	var sky_texture;
	
	/* Dimensions */
	var num_rows;
	var num_columns;
	
	init(level_textfile);
	draw();
};

function level::init(level_textfile){
	wall_texture = CVmTexture("Mattoni04.png");
	wall_material = CVmMaterial();
	wall_material.SetTexture(wall_texture);
	wall_material.ChangeTextureParams(VR_V_TILING, 3);
	
	wall_obj_array = array(0);
	wall_mesh = CVmNewMesh(VRP_BOX);
	wall_mesh.SetMaterial(wall_material);
	wall_mesh.Scale(1,3,1);
	
	lower_wall_mesh = CVmNewMesh(VRP_BOX);
	lower_wall_mesh.SetMaterial(wall_material);
	lower_wall_mesh.Scale(1,1,1);
	
	var f = LoadFile(level_textfile);

	num_rows = len(f);
	num_columns = len(f[0]);
	
	for(var i=0; i< num_rows; i++)
	{
		for(var j=0; j< num_columns; j++)
		{
			if (f[i][j] == "*")
			{
				var t = CVmObj(wall_mesh);
				t.SetPosition(j,0,i);
				aadd(wall_obj_array,t);
			}
			else if(f[i][j] == "+")
			{
				var t = CVmObj(lower_wall_mesh);
				t.SetPosition(j,0,i);
				aadd(wall_obj_array,t);
			}			
		}
	}	
	
	floor_material = CVmMaterial();
	floor_texture  = CVMtexture("grass.jpg");
	floor_material.SetTexture(floor_texture);
	floor_material.ChangeTextureParams(VR_U_TILING,4);
	floor_material.ChangeTextureParams(VR_V_TILING,4);
	
	
	floor_mesh = CVmNewMesh(VRP_BOX);
	floor_mesh.scale(num_columns,1,num_rows);
	floor_mesh.translate(0,-1,0);
	floor_mesh.SetMaterial(floor_material);
	floor_mesh.ModulateMaterials(0,1,0,1);	
	
	sky_mesh = CVmNewMesh("sky.AAM");
	sky_mesh.scale(num_rows*1.5, 1, num_columns*1.5);
	sky_mesh.translate(num_rows / 2.0, 0 , num_columns / 2.0);
}

function level::draw(){
	foreach (var t in wall_obj_array)
		t.Draw();
	floor_mesh.draw();
	sky_mesh.draw();
}